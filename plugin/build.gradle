import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "java"
    id "com.github.johnrengelman.shadow"
}

repositories {
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
}

dependencies {
    implementation project(path: ":justrickycore-api", configuration: "shadow")

    compileOnly "io.papermc.paper:paper-api:1.18.2-R0.1-SNAPSHOT"
    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"

    compileOnly "me.mattstudios:triumph-msg-adventure:2.2.4-SNAPSHOT"
    compileOnly "me.mattstudios:triumph-msg:2.2.4-SNAPSHOT"

    implementation "com.squareup.okhttp3:okhttp:4.9.3"
}

shadowJar {
    archiveBaseName.set("${project.ext.name}-${project.version}")
    archiveClassifier.set("")
    archiveVersion.set("")

    relocate "co.aikar.commands", "${project.ext.groupId}.libs.acf"
    relocate "co.aikar.locales", "${project.ext.groupId}.libs.locales"
}


// replace version information in src/main/resources
processResources {
    filter(ReplaceTokens, tokens: [version: version])
    filter(ReplaceTokens, tokens: [name: project.ext.name])
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = "javac"
}